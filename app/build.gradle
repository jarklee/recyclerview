/*
 * ******************************************************************************
 *  Copyright â’¸ 2016. aimesoft.com. All right reserved
 *  Author TrinhQuan. Create on 2016/10/22
 * ******************************************************************************
 */

buildscript {
    ext.green_dao_version = '3.2.0'
    ext.retrofit_version = '2.1.0'
    ext.android_library_version = '25.0.0'
    ext.butterknife_version = '8.2.1'
    ext.dagger_version = '2.5'
    ext.icepick_version = '2.3.6'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
        classpath "org.greenrobot:greendao-gradle-plugin:$green_dao_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'org.greenrobot.greendao'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

def databaseVersion = 1

def getBuildTime() {
    return System.currentTimeMillis()
}

android {
    compileSdkVersion 24
    buildToolsVersion "25.0.0"
    
    defaultConfig {
        applicationId "com.skeleton.faker"
        minSdkVersion 10
        targetSdkVersion 24
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName String.format("%s.%s.%s", versionMajor, versionMinor, versionPatch)
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + getBuildTime() + "L)"
        buildConfigField "java.lang.String", "AUTHOR", "\"Trinh Quan\""
        buildConfigField "int", "DATABASE_VERSION", "" + databaseVersion

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'  // butterknife
    }
}

greendao {
    schemaVersion databaseVersion
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    //dagger2
    compile "com.google.dagger:dagger:$dagger_version"
    apt "com.google.dagger:dagger-compiler:$dagger_version"
    provided 'javax.annotation:jsr250-api:1.0'
    //event bus
    compile 'org.greenrobot:eventbus:3.0.0'
    //io
    /*compile 'commons-io:commons-io:2.5'*/
    //retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    //butter knife
    compile "com.jakewharton:butterknife:$butterknife_version"
    apt "com.jakewharton:butterknife-compiler:$butterknife_version"
    //reactive
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    //    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    //    compile 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'
    compile 'com.annimon:stream:1.1.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //green dao
    compile "org.greenrobot:greendao:$green_dao_version"
    //ice pick
    compile "com.github.frankiesardo:icepick:$icepick_version"
    provided "com.github.frankiesardo:icepick-processor:$icepick_version"
    //parceler
    compile 'org.parceler:parceler-api:1.1.6'
    apt 'org.parceler:parceler:1.1.6'

    compile 'com.github.jarklee.android_support:androidsupport:1.0.0'
    compile project(':expandable')
}
